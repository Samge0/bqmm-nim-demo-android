/**
 * BQMM集成
 * 在开始使用BQMM SDK之前，开发者需要首先申请到一对AppID和AppSecret，并用它们初始化SDK。
 * 为了运行本Demo，请在项目根目录下的local.properties中加入如下两行：
 * bqmm.appID=<申请到的AppID>
 * bqmm.appSecret=<申请到的AppSecret>
 *
 * 以下代码会从local.properties中读取这两个值
 */
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def bqmmAppID = properties.getProperty('bqmm.appID')
def bqmmAppSecret = properties.getProperty('bqmm.appSecret')

apply plugin: 'com.android.application'

def getGitVersion() {
    try {
        return 'git rev-parse --short HEAD'.execute().text.trim()
    } catch (Throwable th) {
        return "";
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion buildToolsVer
    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 23
        buildConfigField "String", "GIT_REVISION", "\"${getGitVersion()}\""
        buildConfigField "String", "BUILD_DATE", "\"${new Date().toLocaleString()}\"";
        multiDexEnabled true
        /**
         * BQMM集成
         * 以下代码可以把appID和appSecret写入AndroidManifest
         */
        manifestPlaceholders = [bqmmAppID: bqmmAppID, bqmmAppSecret: bqmmAppSecret]
    }

    signingConfigs {
        debug { storeFile file("debug.keystore") }

        release {
            storeFile file('release.keystore')
            storePassword 'thisiskeystorepassword'
            keyAlias 'nim_demo'
            keyPassword 'thisiskeypassword'
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            manifestPlaceholders = [ AMAP_KEY:"09fd4efd3e28e9bf1f449ecec7d34bfe" ]
        }

        release {
            minifyEnabled true
            zipAlignEnabled true
            proguardFile('proguard.cfg')
            signingConfig signingConfigs.release
            manifestPlaceholders = [ AMAP_KEY:"ee20324fba1c7f4ad7a4a207e7f08e8d" ]
        }
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res', 'res-avchat', 'res-chatroom']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs', 'libs-sdk']

        }

    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
    dexOptions {
        preDexLibraries false
        jumboMode true
        javaMaxHeapSize "4g"
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

}

dependencies {
    compile fileTree(include: '*.jar', dir: 'libs')
    compile project(path: ':uikit')
    compile 'com.tencent.bugly:crashreport:latest.release'






    compile 'com.android.support:multidex:1.0.0'
}
